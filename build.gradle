plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '2.0.0'
    // https://github.com/qoomon/gradle-git-versioning-plugin
    id 'me.qoomon.git-versioning' version '6.4.4'
}

description = 'Uploads application artifacts to GitHub release and publishes application manifests'
group 'io.github.itzg'
version '0.0.0-SNAPSHOT'
gitVersioning.apply {
    refs {
        tag('.+') {
            version = '${ref}'
        }
        branch('.+') {
            version = '0.0.0-${ref}-SNAPSHOT'
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

gradlePlugin {
    website = 'https://github.com/itzg/github-releaser-gradle-plugin'
    vcsUrl = 'https://github.com/itzg/github-releaser-gradle-plugin'
    plugins {
        githubReleaser {
            id = 'io.github.itzg.github-releaser'
            displayName = 'Application GitHub Releaser'
            description = project.description
            tags.set(['github'])
            implementationClass = 'io.github.itzg.ghrel.GithubReleaserPlugin'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {

    implementation 'org.kohsuke:github-api:1.330'
    implementation 'commons-codec:commons-codec:1.20.0'
    implementation 'org.apache.commons:commons-text:1.15.0'

    // https://junit.org/junit5/docs/current/user-guide/#running-tests-build-gradle-bom
    testImplementation(platform("org.junit:junit-bom:6.0.1"))
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}